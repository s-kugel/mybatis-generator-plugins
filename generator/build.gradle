plugins {
    id('java')
    id('maven-publish')
    id('com.diffplug.spotless').version('6.20.0')
}

dependencies {
    implementation('org.freemarker:freemarker:2.3.32')
    compileOnly('org.projectlombok:lombok:1.18.28')
    annotationProcessor('org.projectlombok:lombok:1.18.28')
    implementation('org.mybatis.generator:mybatis-generator-core:1.4.2')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding('UTF-8')
}

spotless {
    encoding('UTF-8')
    java {
        indentWithSpaces()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        googleJavaFormat()
    }
}

publishing {
    publications {
        gpr(MavenPublication) {
            groupId = 'com.silber_kugel.mybatis'
            artifactId = 'mybatis-generator-plugins'
            version = '0.0.2'
            from components.java
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/silber-kugel/mybatis-generator-plugins'
            credentials {
                username = findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
